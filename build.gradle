plugins {
    id 'fabric-loom' version "${loom_version}"
    id 'org.jetbrains.kotlin.jvm' version "${kotlin_version}"
    id 'org.jetbrains.kotlin.plugin.serialization' version "${kotlin_version}"
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_24
}

kotlin {
    jvmToolchain(25)
}

repositories {
    mavenCentral()
    maven { url = "https://maven.meteordev.org/releases" }
    maven { url = "https://maven.meteordev.org/snapshots" }
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://maven.fallenbreath.me/releases' }
    maven { url = 'https://masa.dy.fi/maven' }
}

dependencies {
    // Minecraft
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Kotlin
    modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"

    // Meteor
    modImplementation("meteordevelopment:meteor-client:${project.minecraft_version}-SNAPSHOT") {
        changing = true
    }

    //litematica
    // modImplementation "com.github.sakura-ryoko:litematica:LTS~${project.minecraft_version}-SNAPSHOT"
    // modImplementation "com.github.sakura-ryoko:malilib:LTS~${project.minecraft_version}-SNAPSHOT"
    modImplementation "com.github.sakura-ryoko:litematica:${project.minecraft_version}-${project.litematica_version}"
    modImplementation "com.github.sakura-ryoko:malilib:${project.minecraft_version}-${project.malilib_version}"

}

processResources {
    def propertyMap = [
        "version"   : project.version,
        "mc_version": project.minecraft_version,
    ]
    filesMatching("fabric.mod.json") {
        expand(propertyMap)
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding("UTF-8")
}
tasks.register('printVersion') {
    doLast {
        println(project.version)
    }
}
tasks.register('printMCVersion') {
    doLast {
        println(project.minecraft_version)
    }
}

if (file('local.gradle').exists()) {
    apply from: 'local.gradle'
}

if (project.hasProperty('customJarDir')) {
    remapJar {
        destinationDirectory = file(project.customJarDir)
    }
}
